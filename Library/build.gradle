apply plugin: 'com.android.library'

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "com.myriadmobile.library.heroviewpager"
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName project.version
    }
}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
}

//
// Below code was adapted from
// https://github.com/DeployGate/deploygate-android-sdk
//
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
        return; // Skip debug builds.
    }
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task)
}

task androidJar(type: Jar) {
    from android.sourceSets.main.java
}

// central upload
if(gradle.startParameter.taskNames.contains('uploadArchives')) {
    apply from: 'build.publish.gradle'
}
